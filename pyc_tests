#!/bin/tcsh 

#setenv VALGRIND 'valgrind -q '
setenv VALGRIND ' '

cd tests
set failed = 0
foreach g (*.py)
  if (-e $g.flags) then
    set flags = `cat $g.flags`
  else
    set flags =
  endif
  $VALGRIND ../pyc -D.. $flags $g >&! $g.out
  if (-e $g.check) then
    diff $g.out $g.check
  else
    diff $g.out empty
  endif
  if ($?) then
    echo $g "******** COMPILE FAILED ********"
    set failed = `expr $failed + 1`
  else
    echo $g "COMPILE PASSED"
  endif
  if (-e $g.exec.check) then
    $VALGRIND  $g:r >&! $g.exec.out
    diff $g.exec.out $g.exec.check
    if ($?) then
      echo $g "******** EXECUTE FAILED ********"
      set failed = `expr $failed + 1`
    else
      echo $g "EXECUTE PASSED"
    endif
    python $g >&! $g.python.out
    diff $g.exec.out $g.python.out
    if ($?) then
      echo $g "******** CPYTHON VERIFY FAILED ********"
      set failed = `expr $failed + 1`
    endif
  endif
end
echo "---------------------------------------"
if (! $failed) then
  echo "ALL tests PASSED"
else
  echo "********" $failed "test(s) FAILED *********"
endif
cd ..
